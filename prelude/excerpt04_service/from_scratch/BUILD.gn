# Copyright (c) 2018 The Prelude Authors.
# Use of this source code is governed by MIT license that can be
# found in MIT-LICENSE file.

# import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//services/catalog/public/tools/catalog.gni")

# objective:
# 1) build a logger service as an executable
# 2) manually launch the logger process and establish connection
# 3) load catalog json file by path

group("from_scratch") {
  deps = [
    ":logger_executable",
    ":logger_client",
    ":copy_catalog_json"
  ]
}

# executable service target
executable("logger_executable") {
  sources = [
    "//prelude/excerpt04_service/single_class_service_impl/single_class_service_impl.cpp",
    "//prelude/excerpt04_service/single_class_service_impl/single_class_service_main.cpp",
  ]
  deps = [
    "//prelude/excerpt04_service/interfaces",
    "//base",
    "//build/config:exe_and_shlib_deps",
    "//build/win:default_exe_manifest",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/standalone_service:main",
  ]
}

service_manifest("logger_exe_manifest") {
  name = "logger_executable"
  source = "logger_exe_manifest.json"
}

# client
executable("logger_client") {
  sources = [
    "logger_client.cpp",
  ]
  deps = [
    "//prelude/excerpt04_service/interfaces",
    "//base",
    "//mojo/common:common_base",
    "//mojo/edk/system",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/interfaces",
    "//services/service_manager/background:lib",
    "//services/service_manager/runner/common"
  ]
  data_deps = [
    ":logger_executable"
  ]
}

service_manifest("exe_client_manifest") {
  name = "logger_client"
  source = "logger_client_manifest.json"
}

catalog("logger_client_catalog") {
  embedded_services = [ ":exe_client_manifest" ]
  standalone_services = [
    ":logger_exe_manifest",
  ]
}

# copy the generated catalog json file to the output dir
copy("copy_catalog_json") {
  sources = get_target_outputs(":logger_client_catalog")
  outputs = [ "$root_out_dir/logger_client_catalog.json" ]
  deps = [":logger_client_catalog"]
}